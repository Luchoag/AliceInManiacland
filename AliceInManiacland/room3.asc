// room script file

function room_FirstLoad()
{
  gMaingui.Visible=true;
  gAction.Visible=true;
  //cFred.ChangeView(20);
  player.Say("AHHHHHHHHHH!");
  player.LockView(22);
  player.Animate(0, 8, eOnce, eBlock);
  player.UnlockView();
  player.ChangeView(15);
  player.SpeechView=21;
  player.ManualScaling=true;
  player.Scaling=100;
  RemoveWalkableArea(2);
  hGrating.Enabled=false;
}

function hDoor_AnyClick()
{
  if(UsedAction(eGA_LookAt)) {
    player.Say("I must get in the house and find the purple tentacle.");
  } else if (UsedAction(eGA_Open)) {
    player.Walk(385, 100, eBlock);
    player.Say("It's locked.");
  }
  else Unhandled();
}

function oKey_AnyClick()
{
player.FaceObject(oKey);  
  if(UsedAction(eGA_LookAt)) {
    player.Say("Hey! That wasn't here before!");
    player.Say("According to the hamster, that's the key to the house.");
  } else if (UsedAction(eGA_PickUp)) {
    if (player.Scaling==100 || player.Scaling==150) {
      player.Walk(750, 135, eBlock);
      player.Say("I can finally take it.");
      player.AddInventory(iKey);
      oKey.Visible=false;      
    } else if (player.Scaling==30) {
      player.Say("I can't reach it!");
    }  
  }
  else Unhandled();
}

function oBottle_AnyClick()
{
  player.FaceObject(oBottle);
  if(UsedAction(eGA_LookAt)) {
    player.Say("A mysterious green bottle.");
  } else if (UsedAction(eGA_PickUp)) {
    player.Walk(700, 133, eBlock);
    player.AddInventory(iBottle);
    oBottle.Visible=false;
  }
  else Unhandled();
}

function oTable_AnyClick()
{
  player.FaceObject(oTable);
  if(UsedAction(eGA_LookAt)) {
    player.Say("I'm sure that table wasn't here before.");
  } else if (UsedAction(eGA_PickUp) || UsedAction(eGA_Pull)) {
    if (talkedToHamster==false || player.HasInventory(iCake)) {
      player.Walk(730, 133, eBlock);
      oTable.Visible=false;
      oTableUp.Visible=true;
      player.Say("Nothing in here.");
      oTable.Visible=true;
      oTableUp.Visible=false; 
    }
  }
  else Unhandled();
}

function hDoorbell_AnyClick()
{
  player.FaceDirection(eDirectionUp);
  if(UsedAction(eGA_LookAt)) {
    player.Say("I should ring the doorbell.");
  } else if (UsedAction(eGA_Use)) {
    if (player.Scaling==100) {
      player.Walk(355, 98, eBlock);
      RestoreWalkableArea(2);
      cFred.Say("I'm coming!");
      player.Walk(390, 100, eBlock);
      player.FaceDirection(eDirectionUp);
      cFred.Walk(386, 90, eNoBlock);      
    } else if (player.Scaling==30) player.Say("I can't reach it!");
    else if (player.Scaling==150) player.Say("I don't want them to see me like this, I'm huge!");
  }
  else Unhandled();
}

function room_RepExec()
{
  if (cFred.x==386 && cFred.y==90) {
    cFred.Baseline=5;
    cFred.FaceCharacter(player);
    ODoorOpened.Visible=true;    
    player.Walk(390, 100, eBlock);
    player.FaceDirection(eDirectionUp);    
    cFred.Say("Yes? What do you want? I don't want any cookies!");
    player.Say("Cookies? No, I'm not selling cookies.");
    player.Say("My name is Alice, are you the Dr. Fred? I came looking for a walking purple tentacle.");
    cFred.Say("There's no purple or green tentacle in this house! Get out!");
    ODoorOpened.Visible=false;
    cFred.Walk(212, 92, eBlock);
    cFred.ChangeView(24);
    RemoveWalkableArea(2);
    player.Say("How rude!");
    oTable.Visible=true;
    oBottle.Visible=true;
  }
  if (talkedToHamster && !player.HasInventory(iKey)) {
    oKey.Visible=true;
  }
}

function oBushes_AnyClick()
{
  player.FaceObject(oBushes);
  if(UsedAction(eGA_LookAt)) {
    player.Say("Some bushes.");
  } else if (UsedAction(eGA_Use) || UsedAction(eGA_PickUp) || UsedAction(eGA_Pull)) {
    if (oTable.Visible) {
    player.Walk(272, 134, eBlock);
    cHamster.Baseline=2;
    player.Say("I'll take them away.");
    oBushes.Visible=false;
    hGrating.Enabled=true;
   } else {
    player.Say("I should try to get in the house first."); 
   }
  }
  else Unhandled();


}
